apply plugin: 'com.android.application'


android {
    compileSdkVersion 19
    buildToolsVersion "20"

    project.ext.vCode = 1
    try {
        project.ext.vCode = Integer.valueOf(System.getenv("TRAVIS_BUILD_NUMBER"))
    } catch(e) {}

    project.ext.vName = System.getenv("TRAVIS_TAG")
    if(!project.ext.vName) {
        project.ext.vName = "1.0.0"
    } else {
        project.ext.vName = project.ext.vName.replace("v", "")
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode project.ext.vCode
        versionName project.ext.vName
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file("../release.keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_ALIAS")
            keyPassword System.getenv("KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
    }
}

configurations.all {

    resolutionStrategy.cacheChangingModulesFor(1,'hours')
}

configurations {
    compile.exclude group: 'org.apache.httpcomponents'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.jakewharton:butterknife:5.1.0'
    compile 'com.afollestad.silk:silk:1.0.0@aar'
    compile 'com.afollestad.cardsui:CardsUI:1.0.0@aar'
    compile 'com.squareup.picasso:picasso:2.3.2'
    compile 'com.squareup.okhttp:okhttp:2.0.0-RC2'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.+'
    compile 'com.path:android-priority-jobqueue:1.1.2'


    // BEGIN Google APIs

    // Play Services will validate the application prior to allowing OAuth2 access.
    compile(group: 'com.google.android.gms', name: 'play-services', version: '4.4.52') {
        exclude module: 'support-v4'
    }

    // The following lines implement maven imports as defined at:
    // https://code.google.com/p/google-api-java-client/wiki/Setup

    // Android extensions for Google HTTP Client.
    // The following client libraries make HTTP/JSON on Android easier.
    compile(group: 'com.google.http-client', name: 'google-http-client-android',
            version: '1.18.0-rc')



    // Add the Google API client library.
    compile(group: 'com.google.api-client', name: 'google-api-client', version: '1.18.0-rc') {
        // Exclude artifacts that the Android SDK/Runtime provides.
        exclude(group: 'xpp3', module: 'xpp3')
        exclude(group: 'org.apache.httpcomponents', module: 'httpclient')
        exclude(group: 'junit', module: 'junit')
        exclude(group: 'com.google.android', module: 'android')
    }

    // Add the Android extensions for the Google API client library.
    // This will automatically include play services as long as you have download that library
    // from the Android SDK manager.
    // Add the Android extensions for the Google API client library.
    compile(group: 'com.google.api-client', name: 'google-api-client-android',
            version: '1.18.0-rc')
            {
                // Exclude play services, since we're not using this yet.
                exclude(group: 'com.google.android.google-play-services', module: 'google-play-services')
            }

    // END Google APIs





    // This is used by the Google HTTP client library.
    compile(group: 'com.google.guava', name: 'guava', version: '14.0.1')

    compile 'com.appspot.drive_log:geotown:0.2.2-SNAPSHOT'

}
