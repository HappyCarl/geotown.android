apply plugin: 'com.android.application'
apply plugin: 'android-apt'

def AAVersion = '3.1'

apt {
    arguments {
        androidManifestFile variant.processResources.manifestFile
        resourcePackageName 'de.happycarl.geotown.app'
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.0.2'

    project.ext.vCode = 1
    try {
        project.ext.vCode = Integer.valueOf(System.getenv("TRAVIS_BUILD_NUMBER"))
    } catch(e) {}

    project.ext.vName = System.getenv("TRAVIS_TAG")

    if(!project.ext.vName) {
        if(System.getenv("TRAVIS_COMMIT")) {
            project.ext.vName = System.getenv("TRAVIS_COMMIT").substring(0, 10)
        } else {
            project.ext.vName = "Dev"
        }
    } else {
        project.ext.vName = project.ext.vName.replace("v", "")
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode project.ext.vCode
        versionName project.ext.vName
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        preDexLibraries = false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file("../release.keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_ALIAS")
            keyPassword System.getenv("KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles 'proguard-rules.txt'
            signingConfig signingConfigs.release

            resValue "string", "client_id", "server:client_id:1005962513631-78253fgvv2ahe6noj99iepmeccibtlvg.apps.googleusercontent.com"
        }
        debug {
            signingConfig signingConfigs.release

            resValue "string", "client_id", "server:client_id:1005962513631-78253fgvv2ahe6noj99iepmeccibtlvg.apps.googleusercontent.com"
        }
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor(1, 'hours')
}

configurations {
    compile.exclude group: 'org.apache.httpcomponents'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(":common")

    compile ("com.android.support:appcompat-v7:21.+")
    compile ("com.android.support:support-v13" +
            ":21.+")

    apt "org.androidannotations:androidannotations:$AAVersion"
    compile "org.androidannotations:androidannotations-api:$AAVersion"

    compile ('com.readystatesoftware.systembartint:systembartint:1.0.3') {
        exclude module: 'support-v4'
    }
    compile 'de.greenrobot:eventbus:2.2.1'
    compile ('com.afollestad.silk:silk:1.0.0@aar')
    compile ('com.afollestad.cardsui:CardsUI:1.0.0@aar') {
        exclude module: 'support-v4'
    }
    compile 'com.squareup.picasso:picasso:2.3.2'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.path:android-priority-jobqueue:1.1.2'
    compile ('de.cketti.library.changelog:ckchangelog:1.2.0') {
        exclude module: 'support-v4'
    }

    compile('de.keyboardsurfer.android.widget:crouton:1.8.5@aar') {
        exclude module: 'support-v4'
    }

    compile('com.google.android.gms:play-services:6.1.+') {
        exclude module: 'support-v4'
    }

    compile 'com.google.http-client:google-http-client-android:1.18.0-rc'
    compile 'com.google.api-client:google-api-client:1.18.0-rc'
    compile 'com.google.api-client:google-api-client-android:1.18.0-rc'
    compile 'com.appspot.drive_log:geotown:0.4.2-SNAPSHOT'
    compile 'com.google.zxing:android-integration:3.0.0'

    compile('de.psdev.licensesdialog:licensesdialog:1.6.0') {
        exclude module: 'support-v4'
    }
}
